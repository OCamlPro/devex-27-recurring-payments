/*
  Implementation of contract RPSRoot
 */

pragma ton-solidity >= 0.32.0;

pragma AbiHeader expire;
pragma AbiHeader pubkey;

import "../libraries/RPSBase.sol";
import "../interfaces/IRPSRoot.sol";
import "./RPSUser.spp";
import "./RPSProvider.spp";

contract RPSRoot is IRPSRoot {

  uint128 constant MINIMAL_CREDIT = 1 ton ;
  uint128 constant FUNCTION_COST = 0.1 ton ;
  uint128 constant DEPLOY_PROVIDER_COST = 2 ton ;
  uint128 constant DEPLOY_USER_COST = 2 ton ;

  uint32 constant NEED_PROVIDER_CODE = 1 << 0;
  uint32 constant NEED_USER_CODE = 1 << 1;
  
  TvmCell g_provider_code ;
  TvmCell g_user_code ;
  uint32 need_init = NEED_PROVIDER_CODE | NEED_USER_CODE ;
  mapping (uint256 => uint128) g_balances ;
  
  constructor( ) public {
    require( msg.pubkey() == tvm.pubkey(), RPSBase.EXN_AUTH_FAILED );
    tvm.accept();
  }

  function creditBalance( uint256 pubkey ) public override
  {
    require( msg.value > MINIMAL_CREDIT, RPSBase.EXN_NOT_ENOUGH_GAS );
    g_balances[ pubkey ] += msg.value - FUNCTION_COST;
  }

  function deployProvider() public override returns ( address addr )
  {
    require( need_init & NEED_PROVIDER_CODE == 0, RPSBase.EXN_NOT_INITIALIZED );
    uint128 balance ;
    if( msg.sender.value == 0 ){
      balance = g_balances[ msg.pubkey() ];
      require( balance > FUNCTION_COST + DEPLOY_PROVIDER_COST,
               RPSBase.EXN_NOT_ENOUGH_GAS );
      tvm.accept();
      delete g_balances[ msg.pubkey() ];
    } else {
      require( msg.value > FUNCTION_COST + DEPLOY_PROVIDER_COST,
               RPSBase.EXN_NOT_ENOUGH_GAS );
      balance = msg.value;
    }

    addr = new RPSProvider{
    value: balance - FUNCTION_COST,
    varInit: {
      s_owner: msg.sender ,
      s_root: address(this) 
    },
    pubkey: msg.pubkey() ,
    code: g_provider_code
    }( g_user_code );
    
  }

  function deployUser() public override returns ( address addr )
  {
    require( need_init & NEED_USER_CODE == 0, RPSBase.EXN_NOT_INITIALIZED );
    uint128 balance ;
    if( msg.sender.value == 0 ){
      balance = g_balances[ msg.pubkey() ];
      require( balance > FUNCTION_COST + DEPLOY_USER_COST,
               RPSBase.EXN_NOT_ENOUGH_GAS );
      tvm.accept();
      delete g_balances[ msg.pubkey() ] ;
    } else {
      require( msg.value > FUNCTION_COST + DEPLOY_USER_COST,
               RPSBase.EXN_NOT_ENOUGH_GAS );
      balance = msg.value ;
    }

    addr = new RPSUser{
    value: balance - FUNCTION_COST,
    varInit: {
      s_owner: msg.sender ,
      s_root: address(this) 
    },
    pubkey: msg.pubkey() ,
    code: g_user_code
    }( );
    
  }

  function getBalance( uint256 pubkey ) public view override returns ( uint128 )
  {
    return g_balances[ pubkey ];
  }
  
  function getUserCode() public view override returns ( TvmCell )
  {
    require( need_init & NEED_USER_CODE == 0, RPSBase.EXN_NOT_INITIALIZED );
    return g_user_code ;
  }

  function getProviderCode() public override view returns ( TvmCell )
  {
    require( need_init & NEED_PROVIDER_CODE == 0, RPSBase.EXN_NOT_INITIALIZED );
    return g_provider_code ;
  }

  function setUserCode( TvmCell user_code ) public
  {
    require( need_init & NEED_USER_CODE != 0, RPSBase.EXN_ALREADY_INITIALIZED );
    require( tvm.hash( user_code ) ==  0x%{get-code-hash:contract:tvc:RPSUser}, RPSBase.EXN_INVALID_CODEHASH );
    tvm.accept();
    g_user_code = user_code ;
    need_init &= ~NEED_USER_CODE;
  }

  function setProviderCode( TvmCell provider_code ) public 
  {
    require( need_init & NEED_PROVIDER_CODE != 0, RPSBase.EXN_ALREADY_INITIALIZED );
    require( tvm.hash( provider_code ) ==  0x%{get-code-hash:contract:tvc:RPSProvider}, RPSBase.EXN_INVALID_CODEHASH );
    tvm.accept();
    g_provider_code = provider_code ;
    need_init &= ~NEED_PROVIDER_CODE;
  }

  function getProviderAddress( uint256 pubkey, address owner ) public view
    override returns ( address )
  {
    require( need_init & NEED_PROVIDER_CODE == 0, RPSBase.EXN_NOT_INITIALIZED );
    TvmCell stateInit = tvm.buildStateInit({
      contr: RPSProvider,
      varInit: {
        s_root : address(this),
            s_owner : owner,
            },
          pubkey: pubkey,
          code: g_provider_code
        });
    return address( tvm.hash( stateInit ) );
  }
  

  function getUserAddress( uint256 pubkey, address owner ) public view
    override returns ( address )
  {
    require( need_init & NEED_USER_CODE == 0, RPSBase.EXN_NOT_INITIALIZED );
    TvmCell stateInit = tvm.buildStateInit({
      contr: RPSUser,
      varInit: {
        s_root : address(this),
            s_owner : owner,
            },
          pubkey: pubkey,
          code: g_user_code
        });
    return address( tvm.hash( stateInit ) );
  }
  
}

